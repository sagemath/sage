# Setup dependencies that are needed by many modules

sage_install_dir = py.get_install_dir() / 'sage'

inc_numpy = run_command(py,
    [
    '-c',
    '''
import numpy
print(numpy.get_include())
    '''.strip()
    ],
    check: true
  ).stdout().strip()
numpy = declare_dependency(
    include_directories: inc_numpy,
)

inc_cysignals = run_command(py,
    [
    '-c',
    '''
import cysignals
print(cysignals.__file__.replace('__init__.py', ''))
    '''.strip()
    ],
    check: true
  ).stdout().strip()
cysignals = declare_dependency(
    include_directories: inc_cysignals,
)

inc_gmpy2 = run_command(py,
    [
    '-c',
    '''
import gmpy2
print(gmpy2.__file__.replace('__init__.py', ''))
    '''.strip()
    ],
    check: true
  ).stdout().strip()
gmpy2 = declare_dependency(
    include_directories: inc_gmpy2,
)
gmp = dependency('gmp', required: true)

pari = cc.find_library('pari')

inc_cypari2 = run_command(py,
    [
    '-c',
    '''
import cypari2
print(cypari2.__file__.replace('__init__.py', ''))
    '''.strip()
    ],
    check: true
  ).stdout().strip()
cypari2 = declare_dependency(
  include_directories: inc_cypari2,
  dependencies: pari
)

mpfr = cc.find_library('mpfr')

# Cannot be found by pkg-config
flint = cc.find_library('flint')

cblas = dependency('cblas')
gsl = dependency('gsl', fallback: ['gsl', 'gsl_dep'], version : '>=2.5', required: true)
gd = cc.find_library('gd')
iml = cc.find_library('iml')
m = cc.find_library('m')
m4ri = cc.find_library('m4ri')
m4rie = cc.find_library('m4rie')
mtx = cc.find_library('mtx', required: false)
png = cc.find_library('png')
zlib = cc.find_library('z')
intl = cc.find_library('intl', required: false)
curl = cc.find_library('curl')
# Cannot be found via pkg-config
ec = cc.find_library('ec')
ecm = cc.find_library('ecm')
# Cannot be found via pkg-config
glpk = cc.find_library('glpk')
ppl = cc.find_library('ppl')
gmpxx = cc.find_library('gmpxx')
readline = cc.find_library('readline')
lfunction = cc.find_library('Lfunction', required: false)
fflas = cc.find_library('fflas')
givaro = cc.find_library('givaro')
braiding = cc.find_library('braiding')
gc = cc.find_library('gc')
homfly = cc.find_library('homfly')
mpc = cc.find_library('mpc')
mpfi = cc.find_library('mpfi')
mpc = cc.find_library('mpc')
# Cannot be found via pkg-config
gap = cc.find_library('gap')

singular = dependency('Singular')

maxima = find_program('maxima', required: true)

# Cannot be found via pkg-config
ntl = cc.find_library('ntl', required: true)

linbox = declare_dependency(
  dependencies: [cc.find_library('linbox'), ntl]
)

# It's strange but cython cannot find its own include files
# so we find them ourselves, and add them to the include path
inc_cython = run_command(py,
    [
    '-c',
    '''
import Cython
print(Cython.__file__.replace('__init__.py', ''))
    '''.strip()
    ],
    check: true
   ).stdout().strip()
add_project_arguments('-I', inc_cython + 'Includes', language : 'cython')

# Meson currently ignores include_directories for Cython modules, so we
# have to add them manually.
# https://github.com/mesonbuild/meson/issues/9562
add_project_arguments('-I', meson.current_source_dir(), language : 'cython')

# Add global compiler flags
add_project_arguments('-X auto_pickle=False', language : 'cython')
add_project_arguments('-X autotestdict=False', language : 'cython')
add_project_arguments('-X binding=False', language : 'cython')
add_project_arguments('-X c_api_binop_methods=True', language : 'cython')
add_project_arguments('-X cdivision=True', language : 'cython')
add_project_arguments('-X cpow=True', language : 'cython')
add_project_arguments('-X embedsignature=True', language : 'cython')
add_project_arguments('--embed-positions', language : 'cython')
add_project_arguments('-X fast_getattr=True', language : 'cython')
#add_project_arguments('-X language_level="3"', language : 'cython')
add_project_arguments('-X legacy_implicit_noexcept=True', language : 'cython')
add_project_arguments('-X preliminary_late_includes_cy28=True', language : 'cython')

# include search path for cases like 'include "sage/libs/ntl/decl.pxi"'
add_project_arguments('-I', meson.current_source_dir() / '..', language : 'cython')

inc_cpython = include_directories('cpython')
inc_rings = include_directories('rings')
inc_rings_finite = include_directories('rings/finite_rings', 'libs/linkages/padics/relaxed')  # .. is a hack for flint_helper.c
inc_flint = include_directories('libs/flint')
inc_gsl = include_directories('libs/gsl')
inc_ntl = include_directories('libs/ntl')
inc_arb = include_directories('libs/arb')
inc_data_structures = include_directories('data_structures')
inc_ext = include_directories('ext')
inc_interpreters = include_directories('ext/interpreters')
inc_partn_ref2 = include_directories('groups/perm_gps/partn_ref2')
inc_src = include_directories('.')
